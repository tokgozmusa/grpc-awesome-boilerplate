buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.6"
    }
}

plugins {
    id "java"
    id "com.google.protobuf" version "0.8.6"
    id "idea"
    id "application"
}

repositories {
    mavenCentral()
    mavenLocal()
}

def grpcVersion = "1.15.0"
def protocVersion = "3.6.1"
def protoFilesDir = "$projectDir/../../proto-files"

dependencies {
    compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    testCompile "io.grpc:grpc-testing:${grpcVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs "build/generated/source/proto/main/grpc"
            srcDirs "build/generated/source/proto/main/java"
        }
        proto {
            srcDir protoFilesDir
        }
    }
}

startScripts.enabled = false

task helloWorldClient(type: CreateStartScripts) {
    mainClassName = "helloworld.HelloWorldClient"
    applicationName = "hello-world-client"
    outputDir = new File(project.buildDir, "tmp/${mainClassName}")
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(helloWorldClient)
    fileMode = 0755
}
